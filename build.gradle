buildscript {
	if (!hasProperty('nexusPublicUrl')){
		ext.nexusPublicUrl = 'https://epsilony.net/nexus/content/groups/public/'
	}
	repositories{
		jcenter()
		maven{ url nexusPublicUrl }
	}
}

apply plugin: 'eclipse'

eclipse{
	classpath {
		downloadSources=true
		downloadJavadoc=true
	}
}

apply plugin: 'groovy'

dependencies {
	compile gradleApi()
	compile localGroovy()
}

apply plugin: 'maven'

repositories{
	jcenter()
	maven{ url nexusPublicUrl}
}

dependencies { testCompile 'junit:junit:4.12' }

group = 'net.epsilony.gradle'
ext._version = '0.1.0'

if(hasProperty('env')&&env=='production'){
	version=_version
}else{
	version="${_version}-SNAPSHOT"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: nexusDeployReleaseUrl) {
				authentication(userName: nexusDeployUser, password: nexusDeployPassword)
			}
			snapshotRepository(url: nexusDeploySnapshotUrl){
				authentication(userName: nexusDeployUser, password: nexusDeployPassword)
			}
		}
	}
}

sourceCompatibility = '1.7'
